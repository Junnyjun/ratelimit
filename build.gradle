plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'maven-publish'
}

group = 'git.io'
project.version = '0.0.2-SNAPSHOT'

def library_version() {
    def env = project.findProperty("env") ?: "dev"
    println "현재 환경: ${env}"

    // ratelimit가 문자열 "ratelimit"를 의미한다면
    def version = "ratelimit"

    if (env == "dev") {
        version = "${version}-stg"
    }

    return version + ":+"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()

    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/Junnyjun/ratelimit")
        credentials {
            username = System.getenv("USERNAME")
            password = System.getenv("TOKEN")
        }
    }
}

ext {
    set('springCloudVersion', "2023.0.3")
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    implementation 'git.io:' + library_version()

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        prdPublication(MavenPublication) {
            from components.java
            groupId = 'git.io'
            artifactId = 'ratelimit'
            version = project.version
            pom {
                name.set("My Artifact")
                description.set("Ratelimit artifact")
            }
        }
        stgPublication(MavenPublication) {
            from components.java
            groupId = 'git.io'
            artifactId = 'ratelimit-stg'
            version = project.version
            pom {
                name.set("My Artifact")
                description.set("Ratelimit artifact")
            }
        }
    }

    repositories {
        maven {
            name = 'rate'
            url = uri("https://maven.pkg.github.com/Junnyjun/ratelimit")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}