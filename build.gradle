plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'maven-publish'
}

group = 'git.io'
project.version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        stgPublication(MavenPublication) {
            from components.java
            groupId = 'git.io'
            artifactId = 'stg-ratelimit'   // STG용 artifactId
            version = project.version
            pom {
                name.set("My Artifact for STG")
                description.set("STG 환경 전용 artifact")
            }
        }
        ratePublication(MavenPublication) {
            from components.java
            groupId = 'git.io'
            artifactId = 'prd-ratelimit'  // RATE용 artifactId
            version = project.version
            pom {
                name.set("My Artifact for RATE")
                description.set("RATE 환경 전용 artifact")
            }
        }
    }

    repositories {
        maven {
            name = 'stg-rate'
            url = uri("https://maven.pkg.github.com/Junnyjun/ratelimit/stg-rate")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
        maven {
            name = 'rate'
            url = uri("https://maven.pkg.github.com/Junnyjun/ratelimit/prd-rate")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}